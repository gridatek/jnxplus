plugins {
	// Apply the Java Gradle plugin development plugin to add support for developing Gradle plugins
	id 'java-gradle-plugin'

	// Apply the Groovy plugin to add support for Groovy
	id 'groovy'

	id 'maven-publish'

	id 'com.gradle.plugin-publish' version '1.2.0'

	id 'com.diffplug.spotless' version '6.25.0'
}

group = 'io.github.khalilou88.jnxplus'
version = version

repositories {
	// Use Maven Central for resolving dependencies.
	mavenCentral()
}

dependencies {
	// Use the awesome Spock testing and specification framework
	testImplementation 'org.spockframework:spock-core:2.2-groovy-3.0'
}

gradlePlugin {
	// Define the plugin
	website = 'https://khalilou88.github.io/jnxplus/#/gradle-plugin'
	vcsUrl = 'https://github.com/khalilou88/jnxplus'
	plugins {
		jnxplusPlugin {
			id = 'io.github.khalilou88.jnxplus'
			displayName = 'jnxplus gradle plugin'
			description = 'jnxplus gradle plugin for depedency graph'
			tags.set(['nx', 'jnxplus'])
			implementationClass = 'io.github.khalilou88.jnxplus.GradlePlugin'
		}
	}
}

// Add a source set for the functional test suite
sourceSets {
	functionalTest {
	}
}

configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)

// Add a task to run the functional tests
tasks.register('functionalTest', Test) {
	testClassesDirs = sourceSets.functionalTest.output.classesDirs
	classpath = sourceSets.functionalTest.runtimeClasspath
	useJUnitPlatform()
}

gradlePlugin.testSourceSets.add(sourceSets.functionalTest)

tasks.named('check') {
	// Run the functional tests as part of `check`
	dependsOn(tasks.functionalTest)
}

tasks.named('test') {
	// Use JUnit Jupiter for unit tests.
	useJUnitPlatform()
}

spotless {
	groovy {
		// Use the default importOrder configuration
		importOrder()
		// removes semicolons at the end of lines
		removeSemicolons()
		// the Groovy Eclipse formatter extends the Java Eclipse formatter,
		// so it formats Java files by default (unless `excludeJava` is used).
		greclipse() // has its own section below
		// excludes all Java sources within the Groovy source dirs from formatting
		excludeJava()
	}
	groovyGradle {
		target '*.gradle' // default target of groovyGradle
		greclipse()
	}
}
